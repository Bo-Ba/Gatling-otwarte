plugins {
    id 'java'
    id 'idea'
    id "io.gatling.gradle" version '3.10.5.1'
}

group 'com.boba'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://packages.confluent.io/maven/'
    }
}

ext {
    reflectionsVersion= "0.10.2"
}

compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

dependencies {
    gatling 'com.google.cloud:google-cloud-storage:2.37.0'
    gatling "org.reflections:reflections:${reflectionsVersion}"
    gatling "ru.tinkoff:gatling-kafka-plugin_2.13:0.12.0"
}

configurations {
    fatJarDependencies.extendsFrom gatling
}

task fatJar(type: Jar, dependsOn: ['gatlingClasses', 'processResources']) {
    group = "build"
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': 'com.boba.gatling.Application'
    }

    exclude 'META-INF/MANIFEST.MF'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveClassifier = "all"

    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.gatling.output)
    from {
        configurations['fatJarDependencies']
                .collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

tasks.named("jar") { finalizedBy("fatJar") }
